<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.0 Transitional//EN">

<!--Converted with LaTeX2HTML 2002-2-1 (1.71)
original version by:  Nikos Drakos, CBLU, University of Leeds
* revised and updated by:  Marcus Hennecke, Ross Moore, Herb Swan
* with significant contributions from:
  Jens Lippmann, Marek Rouchal, Martin Wilck and others -->
<HTML>
<HEAD>
<TITLE>fixanddive_vb.vb</TITLE>
<META NAME="description" CONTENT="fixanddive_vb.vb">
<META NAME="keywords" CONTENT="examples">
<META NAME="resource-type" CONTENT="document">
<META NAME="distribution" CONTENT="global">

<META NAME="Generator" CONTENT="LaTeX2HTML v2002-2-1">
<META HTTP-EQUIV="Content-Style-Type" CONTENT="text/css">

<LINK REL="STYLESHEET" HREF="examples.css">

<LINK REL="next" HREF="node110.html">
<LINK REL="previous" HREF="node108.html">
<LINK REL="up" HREF="node103.html">
<LINK REL="next" HREF="node110.html">
</HEAD>

<BODY >

<DIV CLASS="navigation"><!--Navigation Panel-->
<A NAME="tex2html1528"
  HREF="node110.html">
<IMG WIDTH="37" HEIGHT="24" ALIGN="BOTTOM" BORDER="0" ALT="next" SRC="next.png"></A> 
<A NAME="tex2html1526"
  HREF="node103.html">
<IMG WIDTH="26" HEIGHT="24" ALIGN="BOTTOM" BORDER="0" ALT="up" SRC="up.png"></A> 
<A NAME="tex2html1520"
  HREF="node108.html">
<IMG WIDTH="63" HEIGHT="24" ALIGN="BOTTOM" BORDER="0" ALT="previous" SRC="prev.png"></A>   
<BR>
<B> Next:</B> <A NAME="tex2html1529"
  HREF="node110.html">lp_vb.vb</A>
<B> Up:</B> <A NAME="tex2html1527"
  HREF="node103.html">Visual Basic Examples</A>
<B> Previous:</B> <A NAME="tex2html1521"
  HREF="node108.html">feasopt_vb.vb</A>
<BR>
<BR></DIV>
<!--End of Navigation Panel-->

<H3><A NAME="SECTION00025600000000000000"></A>
<A NAME="subsubsection:fixanddive_vb.vb"></A>
<BR>
fixanddive_vb.vb
</H3>

<P>
<BR>
<PRE  CLASS="verbatim">' Copyright 2012, Gurobi Optimization, Inc.
'
' Implement a simple MIP heuristic.  Relax the model,
' sort variables based on fractionality, and fix the 25% of
' the fractional variables that are closest to integer variables.
' Repeat until either the relaxation is integer feasible or
' linearly infeasible.

Imports System
Imports System.Collections.Generic
Imports Gurobi

Class fixanddive_vb
    ' Comparison class used to sort variable list based on relaxation
    ' fractionality

    Private Class FractionalCompare : Implements IComparer(Of GRBVar)
        Public Function Compare(ByVal v1 As GRBVar, ByVal v2 As GRBVar) As Integer _
                             Implements IComparer(Of Gurobi.GRBVar).Compare
            Try
                Dim sol1 As Double = Math.Abs(v1.Get(GRB.DoubleAttr.X))
                Dim sol2 As Double = Math.Abs(v2.Get(GRB.DoubleAttr.X))
                Dim frac1 As Double = Math.Abs(sol1 - Math.Floor(sol1 + 0.5))
                Dim frac2 As Double = Math.Abs(sol2 - Math.Floor(sol2 + 0.5))
                If frac1 &lt; frac2 Then
                    Return -1
                ElseIf frac1 &gt; frac2 Then
                    Return 1
                Else
                    Return 0
                End If
            Catch e As GRBException
                Console.WriteLine("Error code: " &amp; e.ErrorCode &amp; ". " &amp; e.Message)
            End Try
            Return 0
        End Function
    End Class

    Shared Sub Main(ByVal args As String())

        If args.Length &lt; 1 Then
            Console.WriteLine("Usage: fixanddive_vb filename")
            Return
        End If

        Try
            ' Read model
            Dim env As New GRBEnv()
            Dim model As New GRBModel(env, args(0))

            ' Collect integer variables and relax them
            Dim intvars As New List(Of GRBVar)()
            For Each v As GRBVar In model.GetVars()
                If v.Get(GRB.CharAttr.VType) &lt;&gt; GRB.CONTINUOUS Then
                    intvars.Add(v)
                    v.Set(GRB.CharAttr.VType, GRB.CONTINUOUS)
                End If
            Next

            model.GetEnv().Set(GRB.IntParam.OutputFlag, 0)
            model.Optimize()

            ' Perform multiple iterations. In each iteration, identify the first
            ' quartile of integer variables that are closest to an integer value
            ' in the relaxation, fix them to the nearest integer, and repeat.

            For iter As Integer = 0 To 999

                ' create a list of fractional variables, sorted in order of
                ' increasing distance from the relaxation solution to the nearest
                ' integer value

                Dim fractional As New List(Of GRBVar)()
                For Each v As GRBVar In intvars
                    Dim sol As Double = Math.Abs(v.Get(GRB.DoubleAttr.X))
                    If Math.Abs(sol - Math.Floor(sol + 0.5)) &gt; 0.00001 Then
                        fractional.Add(v)
                    End If
                Next

                Console.WriteLine("Iteration " &amp; iter &amp; ", obj " &amp; model.Get(GRB.DoubleAttr.ObjVal) &amp; _
                                  ", fractional " &amp; fractional.Count)

                If fractional.Count = 0 Then
                    Console.WriteLine("Found feasible solution - objective " &amp; _
                                      model.Get(GRB.DoubleAttr.ObjVal))
                    Exit For
                End If

                ' Fix the first quartile to the nearest integer value

                fractional.Sort(New FractionalCompare())
                Dim nfix As Integer = Math.Max(fractional.Count / 4, 1)
                For i As Integer = 0 To nfix - 1
                    Dim v As GRBVar = fractional(i)
                    Dim fixval As Double = Math.Floor(v.Get(GRB.DoubleAttr.X) + 0.5)
                    v.Set(GRB.DoubleAttr.LB, fixval)
                    v.Set(GRB.DoubleAttr.UB, fixval)
                    Console.WriteLine("  Fix " &amp; v.Get(GRB.StringAttr.VarName) &amp; " to " &amp; _
                                      fixval &amp; " ( rel " &amp; v.Get(GRB.DoubleAttr.X) &amp; " )")
                Next

                model.Optimize()

                ' Check optimization result

                If model.Get(GRB.IntAttr.Status) &lt;&gt; GRB.Status.OPTIMAL Then
                    Console.WriteLine("Relaxation is infeasible")
                    Exit For
                End If
            Next

            ' Dispose of model and env
            model.Dispose()
            env.Dispose()

        Catch e As GRBException
            Console.WriteLine("Error code: " &amp; e.ErrorCode &amp; ". " + e.Message)
        End Try
    End Sub
End Class
</PRE>
<P>

<DIV CLASS="navigation"><HR>
<!--Navigation Panel-->
<A NAME="tex2html1528"
  HREF="node110.html">
<IMG WIDTH="37" HEIGHT="24" ALIGN="BOTTOM" BORDER="0" ALT="next" SRC="next.png"></A> 
<A NAME="tex2html1526"
  HREF="node103.html">
<IMG WIDTH="26" HEIGHT="24" ALIGN="BOTTOM" BORDER="0" ALT="up" SRC="up.png"></A> 
<A NAME="tex2html1520"
  HREF="node108.html">
<IMG WIDTH="63" HEIGHT="24" ALIGN="BOTTOM" BORDER="0" ALT="previous" SRC="prev.png"></A>   
<BR>
<B> Next:</B> <A NAME="tex2html1529"
  HREF="node110.html">lp_vb.vb</A>
<B> Up:</B> <A NAME="tex2html1527"
  HREF="node103.html">Visual Basic Examples</A>
<B> Previous:</B> <A NAME="tex2html1521"
  HREF="node108.html">feasopt_vb.vb</A></DIV>
<!--End of Navigation Panel-->

</BODY>
</HTML>
